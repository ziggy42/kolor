buildscript {
    ext.kotlin_version = '1.2.61'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

def GROUP_ID = 'com.andreapivetta.kolor'
def ARTIFACT_ID = 'kolor'
def VERSION = '0.0.2'

group GROUP_ID
version VERSION

apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'maven-publish'
apply plugin: 'kotlin'
apply plugin: 'java'

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testCompile 'com.winterbe:expekt:0.5.0'
    testCompile 'org.jetbrains.spek:spek-api:1.1.5'
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId GROUP_ID
            artifactId ARTIFACT_ID
            version VERSION
        }
    }
}

bintray {
    user = project.hasProperty('BINTRAY_USER') ? BINTRAY_USER : ''
    key = project.hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ''
    publications = ['MyPublication']
    pkg {
        repo = ARTIFACT_ID
        name = ARTIFACT_ID
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ziggy42/kolor.git'
        version {
            name = VERSION
            released = new Date()
        }
    }
}